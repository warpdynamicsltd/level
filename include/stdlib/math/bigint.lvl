import stdlib:obj:collections:vector as *;
import stdlib:obj:collections:stack as *;
import stdlib:obj:collections:str as *;

type bigint extends object with rec
(
  var data as vector(u64),
  var sign = 1 as byte
);

sub neg(var a as byte)
{
  if (a == 1) {return 0xff}
  if (a == 0xff) {return 1}
}

method new ()(bigint, var n as u64) as bigint
{
  var this as bigint;
  this.data = vector(u64)(1);
  this.data[0] = n;
  this.sign = 1;
  return this;
}

method new()(bigint, var n = 0 as int) as bigint
{
  var this as bigint;
  this.data = vector(u64)(1);
  if (n < 0)
  {
    this.sign = 0xff;
  }
  else
  {
    this.sign = 1;
  }
  this.data[0] = abs(n);
  return this;
}

method new()(bigint, var a as bigint) as bigint
{
  var this as bigint;
  this.data = vector(u64)(a.data.length());
  this.sign = a.sign;
  for (i = 0; i < a.data.length(); inc i)
  {
    this.data[i] = a.data[i];
  }

  return this;
}

method new ()(bigint, var st as ref(stack(u64))) as bigint
{
  var res as bigint;
  res.data = vector(u64)(st.length());
  res.sign = 1;
  i = 0;
  foreach (var u as u64 in st)
  {
    res.data[i] = u;
    inc i;
  }

  return res;
}

method new add_abs(var this as ref(bigint), var other as ref(bigint)) as bigint
{
  var r as u64;
  var c as u64;
  var a as u64;
  var b as u64;

  var res as bigint;

  st = stack(u64)();

  this_len = this.data.length();
  other_len = other.data.length();
  end_a = false;
  end_b = false;

  i = 0;
  c = 0;
  while (not(end_a and end_b))
  {
    if (i < this_len)
    {
      a = this.data[i];
    }
    else
    {
      a = 0;
      end_a = true;
    }

    if (i < other_len)
    {
      b = other.data[i];
    }
    else
    {
      b = 0;
      end_b = true;
    }

    r = __api__(add, a, b, c);
    if (r == 0 and end_a and end_b)
    {
      break;
    }
    exec st.push(r);

    inc i;
  }

  return bigint(ref st);
}

method new _sub_abs(var this as ref(bigint), var other as ref(bigint)) as bigint
{
  var r as u64;
  var c as u64;
  var a as u64;
  var b as u64;

  var res as bigint;

  st = stack(u64)();

  this_len = this.data.length();
  other_len = other.data.length();
  end_a = false;
  end_b = false;

  i = 0;
  c = 0;
  while (not(end_a and end_b))
  {
    if (i < this_len)
    {
      a = this.data[i];
    }
    else
    {
      a = 0;
      end_a = true;
    }

    if (i < other_len)
    {
      b = other.data[i];
    }
    else
    {
      b = 0;
      end_b = true;
    }

    r = __api__(sub, a, b, c);
    if (r == 0 and end_a and end_b)
    {
      break;
    }
    exec st.push(r);

    inc i;
  }

  return bigint(ref st).normalise();
}

method new sub_abs(var this as ref(bigint), var a as ref(bigint)) as bigint
{
  if (a.lt_abs(this) or this == a)
  {
    return this._sub_abs(a);
  }
  else
  {
    res = a._sub_abs(this);

    if (res == bigint(0))
    {
      res.sign = 1;
    }
    else
    {
      res.sign = 0xff;
    }
    return res;
  }
}

method new +(var this as ref(bigint), var a as ref(bigint)) as bigint
{
  if (this.sign == 1 and a.sign == 1)
  {
    return this.add_abs(a);
  }
  if (this.sign == 1 and a.sign == 0xff)
  {
    return this.sub_abs(a);
  }
  if (this.sign == 0xff and a.sign == 1)
  {
    return a.sub_abs(this);
  }
  if (this.sign == 0xff and a.sign == 0xff)
  {
    res = this.add_abs(a);
    if (res == bigint(0))
    {
      res.sign = 1;
    }
    else
    {
      res.sign = 0xff;
    }
    return res;
  }
}

method new -(var this as ref(bigint)) as bigint
{
  res = bigint(this);
  res.sign = neg(res.sign);
  return res;
}

method new -(var this as ref(bigint), var a as ref(bigint)) as bigint
{
  return [this] + (-[a]);
}

sub new mul(var a as u64, var b as u64) as bigint
{
  var res as bigint;
  var r as u64;
  var d as u64;
  r = __api__(mul, a, b, d);
  if (d == 0)
  {
    res.data = vector(u64)(1);
    res.data[0] = r;
  }
  else
  {
    res.data = vector(u64)(2);
    res.data[0] = r;
    res.data[1] = d;
  }

  return res;
}

method new normalise(var this as ref(bigint)) as bigint
{
  i = this.data.length();
  #if (i == 1 )
  while(i > 1 and this.data[i - 1] == 0)
  {
    dec i;
  }
  var res as bigint;
  res.data = vector(u64)(i);
  for(j = 0; j < i; inc j)
  {
    res.data[j] = this.data[j];
  }

  return res;
}

method new lshift(var this as ref(bigint), var n as int) as bigint
{
  var res as bigint;
  len = this.data.length();
  res.data = vector(u64)(len + n);
  for (i = 0; i < n; inc i)
  {
    res.data[i] = 0;
  }
  for (i = 0; i < len; inc i)
  {
    res.data[n + i] = this.data[i];
  }
  return res;
}

sub new slow_mul_abs(var a as ref(bigint), var b as ref(bigint)) as bigint
{
  res = bigint(0x0);
  n = a.data.length();
  m = b.data.length();
  for (i = 0; i < n; inc i)
  {
    for (j = 0; j < m; inc j)
    {
      res = res.add_abs(mul(a.data[i], b.data[j]).lshift(i + j));
    }
  }

  return res.normalise();
}

method new *(var this as ref(bigint), var a as ref(bigint)) as bigint
{
  res = slow_mul_abs(this, a);
  if (this.sign == a.sign)
  {
    res.sign = 1;
    return res;
  }
  if (this.sign != a.sign)
  {
    if (res == bigint(0))
    {
      res.sign = 1;
    }
    else
    {
      res.sign = 0xff;
    }
    return res;
  }
}

method lt_abs(var this as ref(bigint), var a as ref(bigint)) as bool
{
  if (a.data.length() > this.data.length()) {return true}
  if (a.data.length() < this.data.length()) {return false}
  for (i = this.data.length() - 1; i >= 0; dec i)
  {
    if (this.data[i] < a.data[i]) {return true}
    if (this.data[i] > a.data[i]) {return false}
  }

  return false;
}

method eq_abs(var this as ref(bigint), var a as ref(bigint)) as bool
{
  if (a.data.length() > this.data.length()) {return false}
  if (a.data.length() < this.data.length()) {return false}
  for (i = this.data.length() - 1; i >= 0; dec i)
  {
    if (this.data[i] != a.data[i]) {return false}
  }

  return true;
}

method <(var this as ref(bigint), var a as ref(bigint)) as bool
{
  if (this.sign == 0xff and a.sign == 1) {return true}
  if (this.sign == 1 and a.sign == 0xff) {return false}
  if (this.sign == 1 and a.sign == 1)
  {
    return this.lt_abs(a);
  }
  if (this.sign == 0xff and a.sign == 0xff)
  {
    return a.lt_abs(this);
  }
}

method ==(var this as ref(bigint), var a as ref(bigint)) as bool
{
  return this.eq_abs(a) and this.sign == a.sign;
}

method !=(var this as ref(bigint), var a as ref(bigint)) as bool
{
  return not(this == a);
}

method <=(var this as ref(bigint), var a as ref(bigint)) as bool
{
  return this < a or this == a;
}

method >(var this as ref(bigint), var a as ref(bigint)) as bool
{
  return a < this;
}

method >=(var this as ref(bigint), var a as ref(bigint)) as bool
{
  return a < this or this == a;
}

method new abs(var this as ref(bigint)) as bigint
{
  res = bigint(this);
  res.sign = 1;
  return res;
}

method =(var this as ref(bigint), var a as ref(bigint))
{
  this.data = a.data;
  this.sign = a.sign;
}

method =(var this as ref(bigint), var a as A)
{
  [this] = bigint(a);
}

method new +(var this as ref(bigint), var a as A) as bigint
{
  return [this] + bigint(a);
}

method new +(var this as ref(bigint), var a as A, swap) as bigint
{
  return bigint(a) + [this];
}

method new -(var this as ref(bigint), var a as A) as bigint
{
  return [this] - bigint(a);
}

method new -(var this as ref(bigint), var a as A, swap) as bigint
{
  return bigint(a) - [this];
}

method new *(var this as ref(bigint), var a as A) as bigint
{
  return [this] * bigint(a);
}

method new *(var this as ref(bigint), var a as A, swap) as bigint
{
  return bigint(a) * [this];
}

method ==(var this as ref(bigint), var a as A) as bool
{
  return [this] == bigint(a);
}

method ==(var this as ref(bigint), var a as A, swap) as bool
{
  return [this] == bigint(a);
}

method !=(var this as ref(bigint), var a as A) as bool
{
  return [this] != bigint(a);
}

method !=(var this as ref(bigint), var a as A, swap) as bool
{
  return bigint(a) != [this];
}

method <(var this as ref(bigint), var a as A) as bool
{
  return [this] < bigint(a);
}

method <(var this as ref(bigint), var a as A, swap) as bool
{
  return bigint(a) < [this];
}

method <=(var this as ref(bigint), var a as A) as bool
{
  return [this] <= bigint(a);
}

method <=(var this as ref(bigint), var a as A, swap) as bool
{
  return bigint(a) <= [this];
}

method >(var this as ref(bigint), var a as A) as bool
{
  return [this] > bigint(a);
}

method >(var this as ref(bigint), var a as A, swap) as bool
{
  return bigint(a) > [this];
}

method >=(var this as ref(bigint), var a as A) as bool
{
  return [this] >= bigint(a);
}

method >=(var this as ref(bigint), var a as A, swap) as bool
{
  return bigint(a) >= [this];
}

method del (var this as ref(bigint))
{
  del this.data;
}

method finish(var this as ref(bigint))
{
  del this;
}

method echo (var this as ref(bigint))
{
  res = str();

  var prefix as str;
  if (this.sign == 0xff)
  {
    prefix = str("-");
  }
  else
  {
    prefix = str();
  }

  for (i = this.data.length() - 1; i >= 0; dec i)
  {
    res = res + hex64(this.data[i]);
  }
  echo prefix + res;
}